#Options
GenerateScanner = true
GenerateParser = true
Language = Java
ScannerName = Lexico
ParserName = Sintatico
SemanticName = Semantico
ScannerCaseSensitive = true
ScannerTable = Compact
Input = String
Parser = LL
#RegularDefinitions
mi:[a-z]
ma:[A-Z]
nu:[0-9]
con:s|n|t
aspas: \"
#Tokens
//Palavra reservada
palavra: ({mi}({ma}?{mi})*)

//Identificador
id:_({mi}{ma}?|{ma})(({mi}|{nu}){ma}?)*

//Constante Numerica
connum:{nu}+(\.({nu}+))?

//Constante caractere
concar: '\\{con}'

//Constante literal
conliteral: {aspas}([^\\\"\n])*{aspas}

//ignorar
//Comentario de linha
: "/"/(.)*
:[\t\n\s]


//Palavras reservadas
AND = palavra: "and"
ARRAY = palavra: "array"
DO = palavra: "do"
EXIT = palavra: "exit"
FALSE = palavra: "false"
IF = palavra: "if"
IFFALSEDO = palavra: "ifFalseDo"
IFTRUEDO = palavra: "ifTrueDo"
LITERAL = palavra: "literal"
LOGICAL = palavra: "logical"
NUMBER = palavra: "number"
NOT = palavra: "not"
OR = palavra: "or"
TRUE = palavra: "true"
WRITE = palavra: "write"
READ = palavra: "read"

//Simbolos especiais
":"
","
"["
"]"
"{"
"}"
")"
"("
":="
"+"
"-"
"*"
"/"
"="
"!="
"<"
"<="
">"
">="
#NonTerminals
<programa>
<lista_cmd>
<declaracao_de_variavel>
<tipo>
<simples>
<array>
<arrsize>
<lista_de_identificadores>
<comandoattr>
<lista_id_attr>
<comandoread>
<lista_id_read>
<comandowrite>
<lista_expressoes>
<comandoselect>
<selectopcional>
<comandorep>
<comando>
<expressao>
<elemento>
<relacional>
<operador_relacional>
<aritmetica>
<termo>
<fator>
#Grammar
<programa>::=<declaracao_de_variavel>"{"<lista_cmd>"}";

<lista_cmd>::=î|<comando><lista_cmd>;
<declaracao_de_variavel>::= î|<tipo> ":" <lista_de_identificadores><declaracao_de_variavel>;
<tipo>::=<simples>|<array>;
<simples>::= NUMBER|LITERAL|LOGICAL;
<array>::= ARRAY"["<simples>","<arrsize>"]";
<arrsize>::=connum;
<lista_de_identificadores>::= id|id","<lista_de_identificadores>;
<comandoattr>::=<lista_id_attr> ":=" <expressao>;
<lista_id_attr>::= id|id"["<expressao>"]";
<comandoread>::= READ "("<lista_id_read>")";
<lista_id_read>::=<lista_id_attr>|<lista_id_attr>","<lista_id_read>;
<comandowrite>::=WRITE"("<lista_expressoes>")";
<lista_expressoes>::=<expressao>|<expressao>","<lista_expressoes>;
<comandoselect>::="("<expressao>")" IFTRUEDO "{"<lista_cmd>"}"<selectopcional>;
<selectopcional>::= î|IFFALSEDO "{"<lista_cmd>"}";
<comandorep>::= DO "{"<lista_cmd> IF "("<expressao>")" EXIT <lista_cmd>"}";
<comando>::=<comandoattr>|<comandoread>|<comandowrite>|<comandoselect>|<comandorep>;
<expressao>::=<expressao> AND <elemento> | <elemento> |<expressao> OR <elemento> | <elemento>;
<elemento>::=<relacional> | TRUE | FALSE | NOT <elemento>;
<relacional>::=<aritmetica> <operador_relacional> <aritmetica> | <aritmetica>;
<operador_relacional>::= "=" | "!=" | "<" | "<=" | ">" | ">=";
<aritmetica>::=<aritmetica> "+" <termo> | <aritmetica> "-" <termo> | <termo>;
<termo>::=<termo> "*" <fator> | <termo> "/" <fator> | <fator>;
<fator>::=id |  id "[" <expressao> "]" | connum | concar | conliteral | "(" <expressao> ")" | "+" <fator> | "-" <fator>;


